Sub StockDataAnalyze()

For Each ws In Worksheets
    
  Dim Ticker As String  'Set an initial variable for holding the Ticker name
  Dim Volume As Double 'Set an initial variable for holding the annual stock volum for each Ticker
  Dim OpenPrice As Double 'set an initial variable for holding the open price at the start of the year
  Dim ClosePrice As Double 'set an initial variable for holding the close price by end of the year
  Dim Summary_Table_Row As Integer 'Keep track of the location for each Ticker in the summary table
  
  'Define the variables for the three greatest value
  Dim GreatestPInc As Double
  Dim GreatestPDec As Double
  Dim GreatestVolume As Double
  Dim GreatestIncTicker As String
  Dim GreatestDecTicker As String
  Dim GreatestVolTicker As String
  
      
  'Determine last row
  LastRow = ws.Cells(Rows.Count, 1).End(xlUp).Row
   
   
  Ticker = ws.Cells(2, 1).Value 'set the first Ticker name
  Volume = ws.Cells(2, 7).Value 'initiate Volume for the first Ticker
  OpenPrice = ws.Cells(2, 3).Value 'get the open price of the year for first Ticker
  ClosePrice = ws.Cells(2, 6).Value 'initiate Close Price for the first Ticker

  
  'Initialize the greatest Percent Increase, Percent Decrease and Volume
  GreatestPInc = "0.00"
  GreatestPDec = "0.00"
  GreatestVolume = Volume
  GreatestIncTicker = Ticker
  GreatestDecTicker = Ticker
  GreatestVolTicker = Ticker
    
    
  'place column name in the summary table
  ws.Cells(1, 9).Value = "Ticker"
  ws.Cells(1, 10).Value = "Yearly Change"
  ws.Cells(1, 11).Value = "Percent Change"
  ws.Cells(1, 12).Value = "Total Stock Volume"
  
  'format Yearly Increase column and Yearly Percent Increase column
  ws.Range("J:J").NumberFormat = "0.00"
  ws.Range("K:K").NumberFormat = "0.00"
  
   
  Summary_Table_Row = 2 'set the row for the first cell in the summary table
      
   
  For i = 2 To LastRow
   
       If ws.Cells(i + 1, 1) = ws.Cells(i, 1) Then
       
          Volume = Volume + ws.Cells(i + 1, 7).Value 'Add volume for the current Ticker
          ClosePrice = ws.Cells(i + 1, 6).Value 'Track Close Price for the current Ticker
       
       Else
    
          'Fill in the Summary Table
          ws.Cells(Summary_Table_Row, 9).Value = Ticker
          ws.Cells(Summary_Table_Row, 10).Value = (ClosePrice - OpenPrice)
          ws.Cells(Summary_Table_Row, 12).Value = Volume
          
          If OpenPrice = 0 Then
          
             ws.Cells(Summary_Table_Row, 11).Value = 0
             
          Else
          
             ws.Cells(Summary_Table_Row, 11).Value = (ClosePrice - OpenPrice) / OpenPrice
          
          End If
          
           
           'Find the 3 greatest - greatest Percent Increase, greatest Percent Descrease, and greatest Volume
           
           If (GreatestPInc < ws.Cells(Summary_Table_Row, 11).Value) Then
               
               GreatestPInc = ws.Cells(Summary_Table_Row, 11).Value
               
               GreatestIncTicker = ws.Cells(Summary_Table_Row, 9).Value
           
           End If
           
           If (ws.Cells(Summary_Table_Row, 11).Value < 0) Then
              
              If (Abs(ws.Cells(Summary_Table_Row, 11).Value) > Abs(GreatestPDec)) Then
                  
                  GreatestPDec = ws.Cells(Summary_Table_Row, 11).Value
                  
                  GreatestDecTicker = ws.Cells(Summary_Table_Row, 9).Value
              
              End If
              
           End If
           
           If GreatestVolume < ws.Cells(Summary_Table_Row, 12).Value Then
              
              GreatestVolume = ws.Cells(Summary_Table_Row, 12).Value
              
              GreatestVolTicker = ws.Cells(Summary_Table_Row, 9).Value
           
           End If
           
           
           'Format yearly change
           If ws.Cells(Summary_Table_Row, 10).Value < 0 Then
              
              ws.Cells(Summary_Table_Row, 10).Interior.ColorIndex = 3
           
           ElseIf ws.Cells(Summary_Table_Row, 10).Value > 0 Then
              
              ws.Cells(Summary_Table_Row, 10).Interior.ColorIndex = 4
           
           End If
           
           
           'Format yearly percent change
           ws.Cells(Summary_Table_Row, 11).NumberFormat = "0.00%"
           
           Ticker = ws.Cells(i + 1, 1).Value 'set next Ticker name
           Volume = ws.Cells(i + 1, 7).Value 'initiate Volume for next Ticker
           OpenPrice = ws.Cells(i + 1, 3).Value 'get open price for next Ticker
           ClosePrice = ws.Cells(i + 1, 6).Value 'initiate close price for next Ticker.
           Summary_Table_Row = Summary_Table_Row + 1 'Point to next row in the Summary Table
           
        End If
        
    Next i
    
    'Print 3 greatest - greatest Percent Increase, greatest Percent Descrease, and greatest Volume
    ws.Cells(2, 15).Value = "Greatest % Increase"
    ws.Cells(3, 15).Value = "Greatest % Descrease"
    ws.Cells(4, 15).Value = "Greastest Total Volume"
    ws.Cells(1, 16).Value = "Ticker"
    ws.Cells(1, 17).Value = "Value"
      
    ws.Cells(2, 16).Value = GreatestIncTicker
    ws.Cells(2, 17).Value = GreatestPInc
    ws.Cells(2, 17).NumberFormat = "0.00%"
    
    ws.Cells(3, 16).Value = GreatestDecTicker
    ws.Cells(3, 17).Value = GreatestPDec
    ws.Cells(3, 17).NumberFormat = "0.00%"
    
    ws.Cells(4, 16).Value = GreatestVolTicker
    ws.Cells(4, 17).Value = GreatestVolume
    
Next ws

End Sub

